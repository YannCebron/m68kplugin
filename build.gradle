/*
Copyright 2020 The Authors

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

plugins {
  id 'java'
  id 'org.jetbrains.intellij' version '0.4.17'
  id "org.jetbrains.grammarkit" version "2020.1.2"
}


apply from: "$rootDir/gradle/scripts/verifier.gradle"

group 'com.yanncebron'
version = "${version}"

repositories {
  mavenCentral()
}

dependencies {
  testCompile group: 'junit', name: 'junit', version: '4.13'
}


apply plugin: 'java'
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

def sandboxDir = ".sandbox"

apply plugin: 'idea'
idea {
//    project {
//        jdkName = javaVersion
//        languageLevel = javaVersion
//    }
  module {
    generatedSourceDirs += file('gen')
//    excludeDirs.add(file(intellij.sandboxDirectory))   // todo not working
    excludeDirs += file(sandboxDir)
  }
}

sourceSets {
  main {
    java.srcDirs 'src', 'gen'
    resources.srcDirs 'plugin-resources'
  }
  test {
    java.srcDir 'tests'
  }
}

intellij {
  version intellij_platform_version
  sandboxDirectory = project.rootDir.canonicalPath + "/" + sandboxDir
}


runIde {
  if (project.hasProperty("ideDirectory")) {
    ideDirectory project.property("ideDirectory")
  }
}

buildSearchableOptions {
  enabled false
}

patchPluginXml {
  changeNotes """  
    <b>${version}</b>
    Initial Release - alpha version."""
  untilBuild = null
}


// Lexer generation ===========================================================
apply plugin: 'org.jetbrains.grammarkit'
import org.jetbrains.grammarkit.tasks.GenerateLexer

grammarKit {
  jflexRelease = '1.7.0-2'
  grammarKitRelease = '2019.3'
}

task generateM68KLexer(type: GenerateLexer) {
  source = "grammar/_M68kLexer.flex"
  skeleton = "grammar/idea-flex.skeleton"
  targetDir = "gen/com/yanncebron/m68kplugin/lexer/"
  targetClass = "_M68kLexer"
  purgeOldFiles = true
}


// Plugin verification =========================================================== 
runPluginVerifier {
  pluginFileName = "$rootProject.name-$version"
  ides = ["IC-2019.3"]
//    verifierVersion = "1.222"
}

check.dependsOn runPluginVerifier