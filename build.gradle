/*
Copyright 2020 The Authors

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

plugins {
  id 'java'
  id 'org.jetbrains.intellij' version '0.5.0'
  id "org.jetbrains.grammarkit" version '2020.2.1'
  id 'org.jetbrains.changelog' version '0.6.0'
}


apply from: "$rootDir/gradle/scripts/verifier.gradle"

group 'com.yanncebron'
version = "${version}"

repositories {
  mavenCentral()
}

dependencies {
  implementation group: 'com.atlassian.commonmark', name: 'commonmark', version: '0.15.0'
  implementation group: 'com.atlassian.commonmark', name: 'commonmark-ext-gfm-tables', version: '0.15.0'
  testImplementation group: 'junit', name: 'junit', version: '4.13'
}


apply plugin: 'java'
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

def sandboxDir = ".sandbox"

apply plugin: 'idea'
idea {
//    project {
//        jdkName = javaVersion
//        languageLevel = javaVersion
//    }
  module {
    generatedSourceDirs += file('gen')
//    excludeDirs.add(file(intellij.sandboxDirectory))   // todo not working
    excludeDirs += file(sandboxDir)
  }
}

sourceSets {
  main {
    java.srcDirs 'src', 'gen'
    resources.srcDirs 'plugin-resources'
  }
  test {
    java.srcDir 'tests'
  }
}

intellij {
  version = intellij_platform_version
  sandboxDirectory = project.rootDir.canonicalPath + "/" + sandboxDir
}


runIde {
  if (project.hasProperty("ideDirectory")) {
    ideDirectory = project.property("ideDirectory")
  }
  autoReloadPlugins = false
}

buildSearchableOptions {
  enabled = false
}

apply plugin: 'org.jetbrains.changelog'
patchPluginXml {
  untilBuild = null
  changeNotes({ changelog.latest.toHTML() })
}


// Lexer generation ===========================================================
apply plugin: 'org.jetbrains.grammarkit'
import org.jetbrains.grammarkit.tasks.GenerateLexer

grammarKit {
  jflexRelease = '1.7.0-2'
}

task generateM68KLexer(type: GenerateLexer) {
  source = "src/grammar/_M68kLexer.flex"
  skeleton = "src/grammar/idea-flex.skeleton"
  targetDir = "gen/com/yanncebron/m68kplugin/lexer/"
  targetClass = "_M68kLexer"
  purgeOldFiles = true
}


// Plugin verification =========================================================== 
runPluginVerifier {
  pluginFileName = "$rootProject.name-$version"
  ides = ["IC-2019.3.5", "IC-2020.1.4", "IC-2020.2.3"]
  verifierVersion = "1.252"
}

check.dependsOn verifyPlugin